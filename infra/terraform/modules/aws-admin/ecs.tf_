# IAM resources required for Admin ECS EC2 cluster
resource "aws_iam_role" "admin_ecs" {
  name = "${var.environment}_ecs_instance_role"
  path = "/ecs/"

  assume_role_policy = <<EOF
{
  "Version": "2008-10-17",
  "Statement": [
    {
      "Action": "sts:AssumeRole",
      "Principal": {
        "Service": ["ec2.amazonaws.com"]
      },
      "Effect": "Allow"
    }
  ]
}
EOF

}

resource "aws_iam_instance_profile" "admin_ecs" {
  name = "${var.environment}_ecs_instance_profile"
  role = aws_iam_role.admin_ecs.name
}

resource "aws_iam_role_policy_attachment" "ecs_ec2_role" {
  role       = aws_iam_role.admin_ecs.id
  policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonEC2ContainerServiceforEC2Role"
}

data "aws_iam_policy" "aws_managed_ssm_policy" {
  arn = "arn:aws:iam::aws:policy/service-role/AmazonEC2RoleforSSM"
}

resource "aws_iam_role_policy_attachment" "ecs_ec2_ssm_policy" {
  role       = aws_iam_role.admin_ecs.id
  policy_arn = data.aws_iam_policy.aws_managed_ssm_policy.arn
}

resource "aws_iam_role_policy_attachment" "ecs_ec2_cloudwatch_role" {
  role       = aws_iam_role.admin_ecs.id
  policy_arn = "arn:aws:iam::aws:policy/CloudWatchLogsFullAccess"
}


module "autoscaling" {
  source                       = "../aws-autoscaling"
  name                         = module.infra.ecs_cluster_name
  recreate_asg_when_lc_changes = true

  # Launch configuration
  lc_name              = "${module.infra.ecs_cluster_name}-lc"
  image_id             = data.aws_ami.amazon_linux_2_ecs.id
  instance_type        = var.ecs_instance_type
  security_groups      = [module.infra.internal_ssh_sg_id, module.infra.jenkins_sg_id, module.infra.internal_ecs_sg_id]
  iam_instance_profile = aws_iam_instance_profile.admin_ecs.name
  user_data            = data.template_file.user_data.rendered
  key_name             = var.key_pair_name
  environment          = var.environment
  enable_autoscaling   = var.enable_autoscaling

  root_block_device = [
    {
      volume_size = "100"
      volume_type = "gp2"
    },
  ]

  # Auto scaling group
  asg_name                    = "${module.infra.ecs_cluster_name}-asg"
  vpc_zone_identifier         = module.infra.private_subnets
  associate_public_ip_address = false
  health_check_type           = "EC2"
  desired_capacity            = var.desired_capacity
  min_size                    = 1
  max_size                    = 3
  tags_as_map = {
    Name        = module.infra.ecs_cluster_name
    Environment = var.environment
    Cluster     = module.infra.ecs_cluster_name
    Terraform   = "true"
  }
}
